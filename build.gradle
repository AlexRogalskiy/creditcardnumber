plugins {
  id 'java-library'
  id 'maven'
  id 'signing'
  id 'io.codearte.nexus-staging' version '0.21.0'
  id 'com.gradle.build-scan' version '2.3'
}

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {
  compile 'org.apache.commons:commons-lang3:3.4'
  testCompile 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
  testCompile 'org.hamcrest:hamcrest:2.1'
}

group = 'us.fatehi'
version = '2.2.3'
description = 'Credit Card Number'
sourceCompatibility = '1.8'

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar) {
  archiveClassifier = 'sources'
  from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  archiveClassifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  // archives jar
  archives sourcesJar
  archives javadocJar
}

signing {
  required { gradle.taskGraph.hasTask("uploadArchives") }
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: findProperty('ossrhUsername'), password: findProperty('ossrhPassword'))
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: findProperty('ossrhUsername'), password: findProperty('ossrhPassword'))
      }

      pom.project {
        name 'Credit Card Number'
        packaging 'jar'
        description 'Java library that can provide details of a bank issued credit card number'
        url 'https://github.com/sualeh/credit_card_number'

        scm {
          connection 'scm:git:https://github.com/sualeh/credit_card_number.git'
          developerConnection 'scm:git:https://github.com/sualeh/credit_card_number.git'
          url 'https://github.com/sualeh/credit_card_number.git'
        }

        licenses {
          license {
            name 'GNU Lesser General Public License v3'
            url 'https://www.gnu.org/licenses/lgpl-3.0.en.html'
          }
        }

        developers {
          developer {
            id 'sualeh'
            name 'Sualeh Fatehi'
            email 'sualeh@hotmail.com'
          }
        }
      }
    }
  }

  task publish(type: GradleBuild) {
    tasks = ['clean', 'assemble', 'uploadArchives', 'closeAndReleaseRepository']
  }

  buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'

    publishAlways()
  }

}

